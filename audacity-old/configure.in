dnl
dnl audacity configure.in script
dnl
dnl Joshua Haberman
dnl

dnl Process this file with autoconf to produce a configure script.
AC_INIT
AC_CONFIG_SRCDIR([AudacityApp.h])


dnl Checks for programs.
AC_PROG_CC
AC_PROG_GCC_TRADITIONAL
AC_LANG([C++])
AC_PROG_CXX

dnl extra variables
AC_SUBST(EXTRAOBJS)

dnl options
AC_ARG_WITH(libmpeg3,
	[AC_HELP_STRING([--with-libmpeg3],
      [use libmpeg3 for mp3 support [default=no]])],
	use_libmpeg3=$withval,
	use_libmpeg3="no")
AC_ARG_WITH(xaudio,
	[AC_HELP_STRING([--with-xaudio],
		[use xaudio for mp3 support [default=yes]])],
	use_xaudio=$withval,
	use_xaudio="default") dnl to differentiate between explicit and default
AC_ARG_WITH(vorbis,
	[AC_HELP_STRING([--with-vorbis],
		[enable ogg vorbis support (libraries must be installed) [default=no]])],
	use_vorbis=$withval,
	use_vorbis="no")


dnl check to make sure that all the user's options are valid
AC_CANONICAL_HOST
host_cpu=`echo "$host_cpu" | sed "s/i.86/i386/"`
host_os=`echo "$host_os" | sed "s/-.*//"`

if [[ "$use_libmpeg3" = "yes" ] && [ "$use_xaudio" = "yes" ]] ; then
	AC_MSG_ERROR([*** --use-libmpeg3 and --use-xaudio are mutually exclusive])
fi

if [[ "$use_libmpeg3" = "yes" ]] ; then

	dnl do we need to do a host check?? I have no idea what os's 
	dnl libmpeg3 will compile on...

	AC_DEFINE(USE_LIBMPEG3, 1,
		[Define if mp3 support is implemented with the libmpeg3 library])

	EXTRAOBJS="$EXTRAOBJS mpeg3/libmpeg3.a"
   mp3support="true"
	use_xaudio="no"
fi

if [[ "$use_xaudio" = "yes" ] || [ "$use_xaudio" = "default" ]] ; then

	if [[ "$host_cpu" != "i386" ]] ; then
		AC_MSG_ERROR([*** xaudio is not supported on architectures other than i386])
	fi

	case "$host_os" in
		"linux")
			LIBS="$LIBS -Lxaudio/linux/lib -lxaudio"
			;;
		*)
			AC_MSG_ERROR([*** xaudio is only supported on Linux])
			;;
	esac
			
	AC_DEFINE(USE_XAUDIO, 1,
		[Define if mp3 support is implemented with the xaudio library])
   mp3support="true"
fi

if [[ "$mp3support" = "true" ]] ; then

	AC_DEFINE(MP3SUPPORT, 1,
      [Define if at least one supported library for mp3 functions is present])

fi
dnl this complicated-looking thing is simple a nested function that checks for
dnl the library, checking for the header as well on success
if [[ $use_vorbis = "yes" ]] ; then

AC_CHECK_LIB(vorbisfile, ov_open,
	[ AC_CHECK_HEADER(vorbis/vorbisfile.h, ,
		AC_MSG_ERROR([*** Ogg Vorbis headers not found. libvorbis must be installed for Ogg Vorbis support]))],
	AC_MSG_ERROR([*** Ogg Vorbis libraries not found. libvorbis must be installed for Ogg Vorbis support]),
	-lvorbis -logg) dnl auxiliary library that -lvorbisfile needs

	LIBS="$LIBS -lvorbisfile -lvorbis -logg"
	AC_DEFINE(USE_LIBVORBIS, 1,
		[Define if the ogg vorbis decoding library is present])
fi

AC_PATH_PROG(WX_CONFIG, wx-config, no)
if [[ "$WX_CONFIG" = "no" ]] ; then
	AC_MSG_ERROR("Could not find wx-config: is wxWindows installed?")
fi
LIBS="$LIBS `$WX_CONFIG --libs`"
CFLAGS="$CFLAGS `$WX_CONFIG --cflags`"

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_OFF_T
AC_TYPE_SIZE_T

dnl Checks for library functions.

AC_CONFIG_HEADER(configunix.h:configtemplate.h)
AC_CONFIG_FILES([Makefile])
AC_OUTPUT

