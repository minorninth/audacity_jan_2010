dnl
dnl audacity configure.in script
dnl
dnl Dominic Mazzoni
dnl

dnl Process this file with autoconf to produce a configure script.
AC_INIT(AudacityApp.h)

dnl option
AC_ARG_WITH(libmpeg3,
	[  --with-libmpeg3         use libmpeg3 instead of xaudio for mp3 support],
	  [ AC_DEFINE(USE_LIBMPEG3)
	  AC_CONFIG_SUBDIRS(mpeg3)
	  AC_SUBST(LMDEP, "mpeg3/libmpeg3.a")
	  AC_SUBST(LMRULE, "mpeg3/libmpeg3.a:")
	  AC_SUBST(LMACTION, "	make -C mpeg3")
	  AC_SUBST(XAUDIOLIB, "")],
    [ AC_SUBST(LMDEP, "") 
      AC_SUBST(LMRULE, "")
	  AC_SUBST(LMACTION, "")
	  AC_SUBST(XAUDIOLIB, "-Lxaudio/linux/lib -lxaudio")])

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CC_STDC
AC_PROG_GCC_TRADITIONAL
AC_LANG_SAVE
AC_LANG_CPLUSPLUS
AC_PROG_CXX

dnl Checks for libraries.

dnl wx-config takes care of these:
dnl AC_CHECK_LIB(dl, dlopen)
dnl AC_CHECK_LIB(gdk, gdk_im_open)
dnl AC_CHECK_LIB(gtk, main)
dnl AC_CHECK_LIB(m, main)
dnl AC_CHECK_LIB(pthread, pthread_create)

dnl we don't seem to be using this one at the moment
dnl AC_CHECK_LIB(resolv, main)

AC_PATH_PROG(WX_CONFIG, wx-config, no)
if test "$WX_CONFIG" = "no" ; then
	AC_MSG_ERROR("Could not find wx-config: is wxWindows installed?")
fi
LIBS="$LIBS `$WX_CONFIG --libs`"
CFLAGS="$CFLAGS `$WX_CONFIG --cflags`"


dnl this complicated-looking thing is simple a nested function that checks for
dnl the library, checking for the header as well on success
AC_CHECK_LIB(vorbisfile, ov_open, [
	AC_CHECK_HEADER(vorbis/vorbisfile.h, [
		  LIBS="$LIBS -lvorbisfile -lvorbis -logg"
		  AC_DEFINE(HAVE_LIBVORBISFILE) ],
		AC_MSG_WARN("Ogg Vorbis headers not found. Ogg vorbis support disabled"))],
	AC_MSG_WARN("Ogg Vorbis libraries not found. Ogg vorbis support disabled"),
	-lvorbis -logg) dnl auxiliary library that -lvorbisfile needs

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_OFF_T
AC_TYPE_SIZE_T

dnl Checks for library functions.

AC_OUTPUT(Makefile)
